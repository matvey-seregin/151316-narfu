
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	СканДоговора = ТекущийОбъект.СканДоговора.Получить(); // ДвоичныеДанные
	
	Если СканДоговора <> Неопределено Тогда
		Поток = СканДоговора.ОткрытьПотокДляЧтения();
		ДокументPDF.Прочитать(Поток);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ДействиеСоСканом = ДействиеСохранитьСкан() Тогда
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла("pdf");
		ДокументPDF.Записать(ИмяВременногоФайла);
		ДвоичныеДанные = Новый ДвоичныеДанные(ИмяВременногоФайла);
		//@skip-check empty-except-statement
		Попытка
			УдалитьФайлы(ИмяВременногоФайла);
		Исключение
			// Если файлы не удалились платформа очистит
		КонецПопытки;
		// При попытке работы через потоки генерируется исключение
		// TODO Проверить после обновления платформы
//		Поток = Новый ПотокВПамяти;
//		ДокументPDF.Записать(Поток);
//		ДвоичныеДанные = Поток.ЗакрытьИПолучитьДвоичныеДанные();
		ТекущийОбъект.СканДоговора = Новый ХранилищеЗначения(ДвоичныеДанные);
	КонецЕсли;
	
	Если ДействиеСоСканом = ДействиеОчиститьСкан() Тогда
		ТекущийОбъект.СканДоговора = Новый ХранилищеЗначения(Неопределено);
	КонецЕсли;	

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОчиститьСкан(Команда)
	
	ДокументPDF = Новый ДокументPDF;
	ДействиеСоСканом = ДействиеОчиститьСкан();
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ЗагрузитьСкан(Команда)
	
	ПараметрыПомещения = Новый ПараметрыДиалогаПомещенияФайлов;
	ПараметрыПомещения.Фильтр = "Файл pdf|*.pdf";
	
	Результат = Ждать ПоместитьФайлНаСерверАсинх(,,, ПараметрыПомещения, УникальныйИдентификатор);
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.ПомещениеФайлаОтменено Тогда
		Возврат;
	КонецЕсли;
	
	ПрочитатьСканНаСервере(Результат.Адрес);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПрочитатьСканНаСервере(Адрес)
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(Адрес); // ДвоичныеДанные

	ДокументPDF.Прочитать(ДвоичныеДанные.ОткрытьПотокДляЧтения());
		
	ДействиеСоСканом = ДействиеСохранитьСкан();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ДействиеСохранитьСкан()
	
	Возврат "Сохранить";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ДействиеОчиститьСкан()
	
	Возврат "Очистить";
	
КонецФункции

#КонецОбласти







