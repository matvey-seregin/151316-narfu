
&НаКлиенте
Процедура ОчиститьСкан(Команда)
	
	ДокументPDF = Новый ДокументPDF;
	ДействиеСоСканом = ДействиеОчиститьСкан();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	СканДоговора = ТекущийОбъект.СканДоговора.Получить(); // ДвоичныеДанные
	
	Если СканДоговора <> Неопределено Тогда
		Поток = СканДоговора.ОткрытьПотокДляЧтения();
		ДокументPDF.Прочитать(Поток);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Асинх Процедура ЗагрузитьСкан(Команда)
	
	ДиалогВыбораФайлов = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайлов.Фильтр = "Файл pdf|*.pdf";	

	РезультатВыбора = Ждать ДиалогВыбораФайлов.ВыбратьАсинх();
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДокументPDF.ПрочитатьАсинх(РезультатВыбора[0]);
	
	ДействиеСоСканом = ДействиеСохранитьСкан();

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ДействиеСохранитьСкан()
	
	Возврат "Сохранить";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ДействиеОчиститьСкан()
	
	Возврат "Очистить";
	
КонецФункции

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ДействиеСоСканом = ДействиеСохранитьСкан() Тогда
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла("pdf");
		ДокументPDF.Записать(ИмяВременногоФайла);
		ДвоичныеДанные = Новый ДвоичныеДанные(ИмяВременногоФайла);
		//@skip-check empty-except-statement
		Попытка
			УдалитьФайлы(ИмяВременногоФайла);
		Исключение
			// Если файлы не удалились платформа очистит
		КонецПопытки;
		//Поток = Новый ПотокВПамяти;
		//ДокументPDF.Записать(Поток);
		//ДвоичныеДанные = Поток.ЗакрытьИПолучитьДвоичныеДанные();
		ТекущийОбъект.СканДоговора = Новый ХранилищеЗначения(ДвоичныеДанные);
	КонецЕсли;
	
	Если ДействиеСоСканом = ДействиеОчиститьСкан() Тогда
		ТекущийОбъект.СканДоговора = Новый ХранилищеЗначения(Неопределено);
	КонецЕсли;	

КонецПроцедуры

