
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Процедура заполняет табличный документ для печати.
//
// Параметры:
//	ТабДок - ТабличныйДокумент - табличный документ для заполнения и печати.
//	Ссылка - Произвольный - содержит ссылку на объект, для которого вызвана команда печати.
Процедура Акт(ТабДок, Ссылка) Экспорт

	Макет = ПолучитьМакет("Акт");
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ 
		|	Продажа.Номер,
		|	Продажа.Дата,
		|	Продажа.Контрагент,
		|	Продажа.Организация,
		|	Продажа.Договор,
		|	Продажа.Склад,
		|	Продажа.Ответственный,
		|	Продажа.СписокНоменклатуры.(
		|		НомерСтроки,
		|		Номенклатура,
		|		Цена,
		|		Количество,
		|		Сумма)
		|ИЗ
		|	Документ.Продажа КАК Продажа
		|ГДЕ
		|	Продажа.Ссылка В (&Ссылка)";
		
	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();

	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	Шапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСписокНоменклатурыШапка = Макет.ПолучитьОбласть("СписокНоменклатурыШапка");
	ОбластьСписокНоменклатуры = Макет.ПолучитьОбласть("СписокНоменклатуры");
	Подвал = Макет.ПолучитьОбласть("Подвал");
	
	ТабДок.Очистить();

	ВставлятьРазделительСтраниц = Ложь;
	Пока Выборка.Следующий() Цикл
		Если ВставлятьРазделительСтраниц Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПараметрыЗаголовка = Новый Структура;
		ПараметрыЗаголовка.Вставить("Номер", УдалитьЛидирующиеНули(Выборка.Номер));
		ПараметрыЗаголовка.Вставить("Дата", Формат(Выборка.Дата, "ДЛФ=DD;"));
		ОбластьЗаголовок.Параметры.Заполнить(ПараметрыЗаголовка);
		ТабДок.Вывести(ОбластьЗаголовок);
		
		Шапка.Параметры.Заполнить(Выборка);
		ТабДок.Вывести(Шапка, Выборка.Уровень());	
		ТабДок.Вывести(ОбластьСписокНоменклатурыШапка);
		ВыборкаСписокНоменклатуры = Выборка.СписокНоменклатуры.Выбрать();
		СуммаИтого = 0;
		Пока ВыборкаСписокНоменклатуры.Следующий() Цикл
			ОбластьСписокНоменклатуры.Параметры.Заполнить(ВыборкаСписокНоменклатуры);
			ТабДок.Вывести(ОбластьСписокНоменклатуры, ВыборкаСписокНоменклатуры.Уровень());
			СуммаИтого = СуммаИтого + ВыборкаСписокНоменклатуры.Сумма;
		КонецЦикла;
		
		ПарПредмета ="рубль,рубля,рублей,м,копейка,копейки,копеек,ж,2";
	
		ПараметрыПодвала = Новый Структура;
		ПараметрыПодвала.Вставить("СуммаИтогоПрописью", ЧислоПрописью(СуммаИтого,, ПарПредмета));
		
		Подвал.Параметры.Заполнить(ПараметрыПодвала);
		Подвал.Параметры.Заполнить(Выборка);
		
		ТабДок.Вывести(Подвал);	
		
		ВставлятьРазделительСтраниц = Истина;
		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция УдалитьЛидирующиеНули(Строка)
	
	Результат = Строка;
	Пока СтрНачинаетсяС(Результат, "0") Цикл
		Результат = Сред(Результат, 2);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли
