
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ПроверитьСоответствиеДатыДокументаПериодуДоговора(Отказ);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Ответственный = Пользователи.ТекущийПользователь();
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Ответственный = Пользователи.ТекущийПользователь();
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	// регистр Продажи
	Движения.Продажи.Записывать = Истина;
	Для Каждого ТекСтрокаСписокНоменклатуры из СписокНоменклатуры Цикл
		Движение = Движения.Продажи.Добавить();
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Номенклатура = ТекСтрокаСписокНоменклатуры.Номенклатура;
		Движение.Количество = ТекСтрокаСписокНоменклатуры.Количество;
		Движение.Сумма = ТекСтрокаСписокНоменклатуры.Сумма;
	КонецЦикла;

	// регистр ОстаткиТоваров
	Движения.ОстаткиТоваров.Записывать = Истина;
	Для Каждого ТекСтрокаСписокНоменклатуры из СписокНоменклатуры Цикл
		Движение = Движения.ОстаткиТоваров.Добавить();
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Номенклатура = ТекСтрокаСписокНоменклатуры.Номенклатура;
		Движение.Количество = ТекСтрокаСписокНоменклатуры.Количество;
	КонецЦикла;
	
	Движения.ОстаткиТоваров.БлокироватьДляИзменения = Истина;
	Движения.Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	МИНИМУМ(ПродажаСписокНоменклатуры.НомерСтроки) КАК НомерСтроки,
		|	ПродажаСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(ПродажаСписокНоменклатуры.Количество) КАК Количество
		|ПОМЕСТИТЬ ВТ_ДанныеДокумента
		|ИЗ
		|	Документ.Продажа.СписокНоменклатуры КАК ПродажаСписокНоменклатуры
		|ГДЕ
		|	ПродажаСписокНоменклатуры.Ссылка = &Ссылка
		|СГРУППИРОВАТЬ ПО
		|	ПродажаСписокНоменклатуры.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОстаткиТоваровОстатки.Номенклатура КАК Номенклатура,
		|	ОстаткиТоваровОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ВТ_ДанныеДокумента.Количество КАК Количество,
		|	ВТ_ДанныеДокумента.НомерСтроки КАК НомерСтроки
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваров.Остатки(&Период, Номенклатура В
		|		(ВЫБРАТЬ
		|			ВТ_ДанныеДокумента.Номенклатура
		|		ИЗ
		|			ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента)) КАК ОстаткиТоваровОстатки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента
		|		ПО ОстаткиТоваровОстатки.Номенклатура = ВТ_ДанныеДокумента.Номенклатура
		|ГДЕ
		|	ОстаткиТоваровОстатки.КоличествоОстаток < 0";
		
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Период", Новый Граница(МоментВремени(), ВидГраницы.Включая));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Отказ = Истина;
		
		ШаблонСообщения = "Не хватает товара %1, в наличии %2, необходимо %3";
		ШаблонПоля = "СписокНоменклатуры[%1].Количество";
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.УстановитьДанные(ЭтотОбъект);
		Сообщение.Поле = СтрШаблон(ШаблонПоля, XMLСтрока(Выборка.НомерСтроки - 1));
		Сообщение.Текст = СтрШаблон(ШаблонСообщения, 
			Выборка.Номенклатура, 
			Выборка.КоличествоОстаток + Выборка.Количество,
			Выборка.Количество);
		Сообщение.Сообщить();
		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьСоответствиеДатыДокументаПериодуДоговора(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДоговорыКонтрагентов.ДатаНачала КАК ДатаНачала,
		|	ДоговорыКонтрагентов.ДатаОкончания КАК ДатаОкончания
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|ГДЕ
		|	ДоговорыКонтрагентов.Ссылка = &Ссылка";
		
	Запрос.УстановитьПараметр("Ссылка", Договор);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Выборка.Следующий();
	
	Если Выборка.ДатаНачала > Дата Или КонецДня(Выборка.ДатаОкончания) < Дата Тогда
		Отказ = Истина;
		ШаблонСообщения = "Дата документа должна быть в периоде действия договора (с %1 по %2)";
		Сообщение = Новый СообщениеПользователю;
		Сообщение.УстановитьДанные(ЭтотОбъект);
		Сообщение.Поле = "Договор";
		Сообщение.Текст = СтрШаблон(ШаблонСообщения, 
			Формат(Выборка.ДатаНачала, "ДЛФ=DD;"), 
			Формат(Выборка.ДатаОкончания, "ДЛФ=DD;"));
		Сообщение.Сообщить();
	КонецЕсли;		
	
КонецПроцедуры

#КонецОбласти

#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
